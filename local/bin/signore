#!/bin/perl
# signore - run a command with specific signal disposition

use warnings;
use strict;
use Getopt::Long qw(:config bundling no_ignore_case);

sub usage {
    print "usage: [-hl] [-d signal] [-i signal] command\n";
}

my @opt_default;
my @opt_ignore;

GetOptions(
    "h|help"      => sub {usage(); exit(0);},
    "l|list"      => sub {foreach (keys %SIG) {print "$_\n"}; exit(0);},
    "i|ignore=s"  => \@opt_ignore,
    "d|default=s" => \@opt_default,
) || exit(1);

my @bad_signals = grep {!exists $SIG{$_}} (@opt_default, @opt_ignore);

foreach (@bad_signals) {
    print STDERR "$_: unknown signal\n";
}

if (@bad_signals) {
    die("try -l for a list of known signals\n");
}

$SIG{$_} = "IGNORE" for @opt_ignore;
$SIG{$_} = "DEFAULT" for @opt_default;

exec(@ARGV);
